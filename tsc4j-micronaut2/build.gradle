//
// build.gradle
//

description = "Micronaut 2.x framework integration"
publishing.publications.mavenJava.pom.description = rootProject.name + ' :: ' + description

// required for spock tests to execute successfuly
// see: https://stackoverflow.com/questions/50681096/groovy-2-5-0-gives-noclassdeffounderror-for-methodcalltransformation
ext['groovy.version'] = "${groovy3Version}"

dependencyManagement {
  imports {
    mavenBom "io.micronaut:micronaut-bom:${micronaut2Version}"
    mavenBom "org.spockframework:spock-bom:${spock2Version}"
  }
}

dependencies {
  annotationProcessor     "io.micronaut:micronaut-inject-java"

  api                     project(":${rootProject.name}-core")

  api                     "io.micronaut:micronaut-inject"
  compileOnly             "io.micronaut:micronaut-inject-java"
  compileOnly             "io.micronaut:micronaut-runtime"


  // abstract tests from core subproject
  testImplementation      project(path: ":${rootProject.name}-core", configuration: "testOutput")

  testImplementation      ("org.codehaus.groovy:groovy-test") { version { strictly "${groovy3Version}" } }
  testImplementation      ("org.codehaus.groovy:groovy") { version { strictly "${groovy3Version}" } }

  testAnnotationProcessor "io.micronaut:micronaut-inject-groovy"
  testImplementation      "io.micronaut:micronaut-http-server-netty"
  testImplementation      "io.micronaut:micronaut-inject-groovy"

  testImplementation      "io.micronaut.test:micronaut-test-spock"
  testImplementation      "io.micronaut.test:micronaut-test-junit5"
}

test {
  useJUnitPlatform()
}

// vim:shiftwidth=2 softtabstop=2 expandtab
// EOF
